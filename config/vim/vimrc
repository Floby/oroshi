" RESET  {{{
" We do not need the backward compatibility with vi, so it is better to turn
" this off for better portability of plugins.
set nocompatible
" Turn it off for security reasons, we don't want arbitrary code executed from
" plugins
set nomodeline
" Disabling filetype before invoking Pathogen
filetype off
" Initializing pathogen, loading all plugins located in ~/.vim/bundle
call pathogen#infect()
"  Loading the plugin documentation
call pathogen#helptags()
" Re-enabling filetype indentation
filetype plugin indent on
"}}}
" FILES {{{
" Use backup, and save it in a custom dir
set backup
set backupdir=~/.vim/backup/
" Saving the latest undo in a file on disk
set undofile
set undolevels=1000
set undodir=~/.vim/undo/
" Save all the views (cursor position, fold status) in the same dir, close to
" the others
set viewdir=~/.vim/view/
" We do not want vim to keep swap files. It tends to pollute dirs, and I've
" never come accross a case that it actually was useful to me.
set noswapfile
" }}}
" BASIC  {{{
" Switch working directory to the directory of the currently edited file
set autochdir
" Workaround autocmd to manually switch directory
augroup autochdir
	au!
	autocmd BufEnter * silent! lcd %:p:h
augroup END
" Saving files whenever I move from one to another
set autowrite
" Read files as UTF-8
setglobal fileencoding=utf-8
" Save as UTF-8
set encoding=utf-8
" Force Unix line ending on new file
set fileformats=unix,dos,mac
" keep - and _ as part of words
set iskeyword+=-,_
" Allow multiple buffers opened at once
set hidden
" Reload files if changed outside of vim (every 2s, or on tab change)
set autoread
set updatetime=2000
augroup autoread
	au!
	autocmd CursorHold * checktime
	autocmd TabEnter * checktime
augroup END
" Set +x to script files on save
augroup ft_add_chmodx
	au!
	function! AddExecutablePermissionIfScript()
		if getline(1) =~ "^#!" && getline(1) =~ "/bin/"
			silent !chmod +x <afile>
		endif
	endfunction
	au BufWritePost * call AddExecutablePermissionIfScript()
augroup END
" Reload the vimrc file whenever it is edited
augroup ft_vimrc
	au!
	au BufWritePost *vimrc mkview | source ~/.vimrc | loadview
augroup END
"}}}
" VIEWS {{{
" We only want the cursor and folds (not the custom options)
set viewoptions=cursor,folds
" Save sessions on close, load it on open
augroup views
	au!
	au BufWinLeave * silent! mkview!
	au BufWinEnter * silent! loadview
augroup END
" }}}
" DISPLAY {{{
" Improve display time on slow terminals
set ttyfast
set lazyredraw
" Display line numbers in left column
set number
" Adds an -- INSERT -- or -- VISUAL -- text whenever we enter on of these
" modes.
set showmode
" Show more info about current command
set showcmd
" Always keep 3 lines visible to keep context
set scrolloff=8
" Hide invisible chars by default, but if they should be displayed, define the
" characters to define them.
set nolist
set listchars=tab:â–¸\ ,eol:Â¬,trail:Â·
" Chars used for GUI
set fillchars=stl:\ ,stlnc:\ ,vert:\|,fold:\ ,diff:-
" }}}
" FUNCTIONS {{{
source ~/.vim/includes/functions.vim
" }}}

" KEYBINDINGS {{{
source ~/.vim/includes/keybindings.vim
" }}}

" AUTOCOMPLETION {{{
" Display autocomplete suggestion in a menu if more than one
set completeopt=menu
" Update the menu with matches as I type
set completeopt+=longest
" }}}
" COLORS   {{{
" Enabling syntax highlighting
syntax on
colorscheme oroshi
" Coloring current line
set cursorline
" Reloading the colorscheme
function! ReloadColorscheme(scheme)
	silent! mkview!
	" Reloading colorscheme
	" syn off
	call rainbow_parentheses#toggle()
	execute 'colorscheme '.a:scheme
	call rainbow_parentheses#toggle()
	" syn on
	silent! loadview
endfunction
" Switching between light and dark themes with F2 and Ctrl+F2
" Note : O1;5Q stands for Ctrl+F2 as <C-F2> does not seem to work
nnoremap <silent> <F2> :call ReloadColorscheme('oroshi')<CR>
inoremap <silent> <F2> <Esc>:call ReloadColorscheme('oroshi')<CR>li
nnoremap <silent> O1;5Q :call ReloadColorscheme('summerfruit')<CR>
inoremap <silent> O1;5Q <Esc>:call ReloadColorscheme('summerfruit')<CR>li
" Display the current highlight group of the word under cursor
function! Debugcolor()
	for id in synstack(line("."), col("."))
		execute 'verbose hi '.synIDattr(id, "name")
	endfor
	execute 'verbose hi '.synIDattr(synIDtrans(id), "name")
endfunction
nnoremap <F3> :call Debugcolor()<CR>
"}}}
" COMMANDS {{{
" Keep more commands in history
set history=1000
" Save file using sudo
cnoremap w!! w !sudo tee '%' >/dev/null<CR><CR>
" }}}
" FOLDING  {{{
source ~/.vim/includes/folding.vim
"}}}
" INDENTATION  {{{
" We use the advanced indentation method provided by the filetype plugin
set nosmartindent
filetype plugin indent on
" autoindent is still needed to correctly indent list items
set autoindent
" Using two spaces for indentation as a default. Using tabs can be overwritten
" in language specific-files if needed.
set tabstop=2
set shiftwidth=2
set expandtab
" Lines too long to fit on one screen will be truncated.
set nowrap
let &showbreak='â†ª '
" Force a 79 column policy with an auto-break on words in comments to assure
" maximum readability. Visually add a column to see where the cut will be.
set linebreak
set colorcolumn=80
set textwidth=79
" Create two format modes, for code and for text
" c : Comments are wrapped based on textwidth
" r : New line in comment is a comment when pressing <Enter>
" o : New line in comment is a comment when pressing o or O
" n : Handles list while formatting
" q : Comments can be re-wrapped using gq
" 1 : Do not end lines with one-char words
set formatoptions=cronq1
" We make sure that backspace in insert mode can delete new lines and tabs
set backspace=indent,eol,start
" Format whole paragraph
nnoremap gqp vipgq
"}}}
" SEARCH / REPLACE  {{{
"  Searchs are case-insensitive, unless an uppercase is present
set ignorecase
set smartcase
" Results are highlighted as we type as well as when found. ,<Space> clears the
" highlight
set incsearch
set hlsearch
nnoremap <silent> <Leader><Space> :nohlsearch<CR>
" Regexps will use the extended format (no need to escape special chars to give
" them meaning - (.*) will work when \(.*\) was needed before. Also, all
" matches are global on the line, not limited to the first one.
set gdefault
nnoremap / /\v
nnoremap ? ?\v
nnoremap :s/ :s/\v
nnoremap :%s :%s/
nnoremap :%S :%s/
nnoremap :Ã¹s :%s/
"}}}
" SNIPPETS   {{{
" Lorem ipsum
iabbrev lorem Lorem ipsum dolor sit amet, consectetur adipiscing elit
iabbrev llorem Lorem ipsum dolor sit amet, consectetur adipiscing elit.  Etiam lacus ligula, accumsan id imperdiet rhoncus, dapibus vitae arcu.  Nulla non quam erat, luctus consequat nisi
iabbrev lllorem Lorem ipsum dolor sit amet, consectetur adipiscing elit.  Etiam lacus ligula, accumsan id imperdiet rhoncus, dapibus vitae arcu.  Nulla non quam erat, luctus consequat nisi.  Integer hendrerit lacus sagittis erat fermentum tincidunt.  Cras vel dui neque.  In sagittis commodo luctus.  Mauris non metus dolor, ut suscipit dui.  Aliquam mauris lacus, laoreet et consequat quis, bibendum id ipsum.  Donec gravida, diam id imperdiet cursus, nunc nisl bibendum sapien, eget tempor neque elit in tortor
" Ultisnips
" }}}
" SPLIT  {{{
" Splitting right and bottom feels more natural
set splitright
set splitbelow
" Use Arrow in normal mode to move accros splits
nnoremap <Up> <C-W>k
nnoremap <Right> <C-W>l
nnoremap <Down> <C-W>j
nnoremap <Left> <C-W>h
"}}}
" STATUS LINE  {{{
source ~/.vim/includes/statusline.vim
"}}}
" TABS   {{{
" Do not limit the number of tabs to open when launching vim
set tabpagemax=1000
" Open file in new tab with ,t
nnoremap <Leader>t :tabe<Space>
" Switch tab with movement keys
nnoremap <C-H> gT
nnoremap <C-L> gt
inoremap <C-H> <Esc>gT
inoremap <C-L> <Esc>gt
cnoremap <C-H> <C-C>gT
cnoremap <C-L> <C-C>gt
"}}}
" WILDMENU {{{
" We enable a simple wildmenu with basic Tab-completion.
set wildmenu
" We ignore some files and dir that we're sure we'll never edit in vim
set wildignore=''
set wildignore+=*.orig,*.swp,*.crcdownload            " Tmp files
set wildignore+=*.jpg,*.jpeg,*.png,*.ico,*.gif,*.pdf  " Image files
set wildignore+=*.exe,*.dll,*.pyc,*.so                " Binaries
set wildignore+=*.zip,*.rar,*.tgz,*.tar,*.cbz         " Archive files
" }}}
" YANK / PASTE   {{{
" Note: Systems have two different clipboards.
" unnamed is (select text / middle click) and accessed with "*
" unnamedplus is (Ctrl+C / Ctrl+V) and accessed with "+
" We only use unnamed plus, if available
set clipboard=
if has("unnamedplus")
	set clipboard+=unnamedplus
	function! PrePaste()
		" We guess that if both + and * registers have the same number of words,
		" and + is longer than *, it means they are actually the same source, but
		" + gets garbled, so we'll use * in place of +
		let lenstar = len(@*)
		let wordstar = len(split(@*))
		let lenplus = len(@+)
		let wordplus = len(split(@+))

		" If @+ is empty, we use @*
		if lenplus == 0 && lenstar > 0
			let @+=@*
		endif

		" If about same size, but @+ is bigger than @* we guess that it gets
		" garbled and we replace it by @*
		if wordstar !=1 && wordstar == wordplus && lenstar < lenplus
			let @+=@*
		endif
	endfunction
	" noremap <silent> p :call PrePaste()<CR>p
	" noremap <silent> P :call PrePaste()<CR>P
endif
" As gv selects last selected text, we add gp to select last pasted text
nnoremap gp `[v`]
" We'll use x as a way to delete text without keeping it in a paste buffer
nnoremap x "_x
vnoremap x "_x
" [c]hanging a word does not put it in yank buffer
nnoremap c "_c
"}}}

" CTRL-F {{{
source ~/.vim/includes/ctrlf.vim
" }}}
" CTRL-P {{{
source ~/.vim/includes/ctrlp.vim
" }}}
" RAINBOW PARENTHESES {{{
" Load plugin
au VimEnter * if exists(":RainbowParenthesesToggle") | execute 'RainbowParenthesesToggle' | endif
" }}}
" GIT {{{
source ~/.vim/includes/git.vim
" }}}
" SYNTASTIC {{{
let g:syntastic_auto_loc_list=0
let g:syntastic_always_populate_loc_list=1
let g:syntastic_error_symbol='âœ—âœ—'
let g:syntastic_warning_symbol='âš âš '
let g:syntastic_stl_format='%E{E:%e}%B{ }%W{W:%w}'
let g:syntastic_mode_map={ 'mode': 'passive',
                     \ 'active_filetypes': ['javascript', 'json', 'css', 'html', 'ruby'],
                     \ 'passive_filetypes': [] }
" }}}
" FILETYPES {{{
" Load skeletons
augroup skeletons
	au!
	au BufNewFile * :silent! exec ":0r ~/.vim/includes/skeletons/skeleton.".expand('%:e')
augroup END

" ,ve to edit vimrc
nnoremap <leader>ve :tabe ~/.oroshi/config/vim/vimrc<CR>
" ,vr to reload vimrc
nnoremap <leader>vr :source ~/.vimrc<CR>:let &filetype=&filetype<CR>:loadview<CR>:nohlsearch<CR>

" man to open the manpage of a command in a new tab
nnoremap <silent> man :tabe<CR>:setlocal filetype=man<CR>:.!man<Space>
" }}}


" Rsync kiss front-end stuff to tomcat server
augroup ft_kiss
	let kissihmapp="/var/www/java/kissihm/kissihm/src/main/webapp/resources/app/*" 
	let kissihmwebapp="/var/lib/tomcat7/webapps/kiss/resources/app/"
	au BufWritePost /var/www/java/kissihm/kissihm/src/main/webapp/resources/app/* execute ':silent !rsync -ra --delete '.kissihmapp.' '.kissihmwebapp
augroup END
