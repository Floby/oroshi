#compdef bchunk
# - Will first suggest the bin file
# - Then the cue one
# - And finally a basename based on the bin name

_bchunk () {
	# local var definition to not pollute global scope
	local curcontext=$curcontext state line context
	typeset -A opt_args

	# suggesting arguments
	_arguments \
		"1:bin file:->bin_file" \
		"2:cue file:->cue_file" \
		"3:output file basename:->basename_file" \
		&& return 0

	# state machine match
	case $state in
		# First, only suggest .bin files
		(bin_file)
			_files -g '*.(bin|raw|img)' && return 0
		;;
		# Then, only suggest the .cue file
		(cue_file)
			_files -g '*.cue' && return 0
		;;
		# Finally, suggest a basename based on the original name
		(basename_file)
			compadd "$@" ${words[2]:r}
			return 0
		;;
	esac

	return 1
}

_bchunk "$@"
