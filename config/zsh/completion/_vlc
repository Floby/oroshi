#compdef vlc
# - Will only suggest movie files
# - Will also suggest subtitles files with the --sub-file option

_vlc () {
	# local var definition to not pollute global scope
	local curcontext=$curcontext state line context
	typeset -A opt_args

	# suggesting arguments
	_arguments \
		"*:media file:->media_file" \
		"--sub-file[subtitle file to use]:subtitle file:->subtitle_file" \
		&& return 0

	# state machine match
	case $state in
		# Suggesting the media file to open. This will suggest only video and audio
		# files
		(media_file)
			local glob=$(echo $O_FILETYPES_EXTENSIONS[video],$O_FILETYPES_EXTENSIONS[audio] | sed 's/,/|/g')
			_files -g '*.('$glob')' && return 0
		;;
		# Suggesting a subtitle file.
		(subtitle_file)
			local glob=$(echo $O_FILETYPES_EXTENSIONS[subtitle] | sed 's/,/|/g')
			_files -g '*.('$glob')' && return 0
		;;
	esac

	return 1
}

_vlc "$@"
