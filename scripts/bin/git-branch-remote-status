#!/usr/bin/env zsh
# Check the status of the current branch against its remote counterpart.
# Will return a different exit code in each case :

# 0. Local is same as remote
local EXIT_CODE_IDENTICAL=0
# 1. Local is ahead of remote (need to push)
local EXIT_CODE_AHEAD=1
# 2. Local is behind remote (need to pull)
local EXIT_CODE_BEHIND=2
# 3. Local and remote have diverged
local EXIT_CODE_DIVERGED=3
# 4. Local has never been pushed to server
local EXIT_CODE_NEVER_PUSHED=4

local remoteHead=$(git rev-parse @{upstream} 2>/dev/null)
# Remote has no hash, means it never get pushed
if [[ "$remoteHead" = '@{upstream}' ]]; then
  exit $EXIT_CODE_NEVER_PUSHED
fi

local localHead=$(git rev-parse @{0} 2>/dev/null)
# Local is same as remote
if [[ "$remoteHead" = "$localHead" ]]; then
  exit $EXIT_CODE_IDENTICAL
fi

local remoteMergeBase=$(git merge-base @{0} @{upstream} 2>/dev/null)
# Merge base is the same as remote, it means we are ahead
if [[ "$remoteHead" = "$remoteMergeBase" ]]; then
  exit $EXIT_CODE_AHEAD
fi

# Merge base is the same as local, it means we are behind
if [[ "$localHead" = "$remoteMergeBase" ]]; then
  exit $EXIT_CODE_BEHIND
fi

# Any other case means we've diverged
exit $EXIT_CODE_DIVERGED
