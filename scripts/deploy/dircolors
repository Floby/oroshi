#!/usr/bin/zsh
# Creates a ~/.dircolors files containing default colors as well as custom
# colors for extensions defined in ~/.oroshi/config/zsh/filetypes.db.zsh

# Paths
local configDir=~/.oroshi/config/dircolors
local defaultFile=${configDir}/dircolors-default
local customFile=${configDir}/dircolors
local dbFile=~/.oroshi/config/zsh/filetypes.db.zsh
local destFile=~/.dircolors

# Create initial file
cp -f $defaultFile $destFile
cat $customFile >> $destFile

# We source the file holding our filetype definition
source $dbFile

# We add custom coloring for each extension defined
for ft in ${(k)O_FILETYPES}; do
	# We get the color used for this filetype
	ft_color=$(echo $O_FILETYPES[$ft] | awk -F: '{ print $2 }')
	# And the list of extensions, as a ZSH array
	ft_extensions=$(echo $O_FILETYPES[$ft] | awk -F: '{ print $3 }')

	# We add a header line in the ~/.dircolors file
	echo "# $ft files" >> $destFile

	# We now add a line for each extension
	for ext in ${(s/,/)ft_extensions}; do
		echo ".$ext $ft_color" >> $destFile
		# And the same goes for uppercase extensions
		echo ".${ext:u} $ft_color" >> $destFile
	done
done
